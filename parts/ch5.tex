
\chapter{Designing a procedure for synthesizing QPFs from specifications}%
\label{ch:procedure}

This chapter will establish a procedure that compiles a specification of a (co)datatype
into the proper constructions on QPFs.
It will do so in the abstract, focusing on the details of the procedure, rather than the implementation
details of the Lean meta-programming system, which will be covered in the next chapter.


\section{Shape types}%
\label{sec:shape_types}

Arguably the simplest, and most fundamental, inductive types are \leanm{Sum α β} and \leanm{Prod α β},
representing ``either α or β'' and ``a pair of α and β'', respectively.
They can be defined as

\begin{center}
\begin{minipage}[t]{0.45\linewidth}
    \begin{leancode}
inductive Sum α β
  | inl : α → Sum α β
  | inr : β → Sum α β
    \end{leancode}
\end{minipage}
\begin{minipage}[t]{0.45\linewidth}
    \begin{leancode}    
inductive Prod α β
  | mk : α → β → Prod α β
    \end{leancode}
\end{minipage}
\end{center}

They are also examples of what we will call \emph{shape} types.
\begin{definition}
    A \emph{shape} type is an inductive type \lean{Foo α\_1 \ldots\ α\_n}, 
    where each constructor takes only arguments of types in $\{α_1, \ldots, α_n\}$.
\end{definition}
That is, each constructor's arguments must be typed as one of the parameters of the shape type.
Let's make this a bit clearer by looking at examples that are \textbf{not} shape types. 

%LEAN namespace BadExample
\begin{leancode}
  inductive List α
    | nil  : List α 
    | cons : α → List α → List α

  inductive ListWrapper α
    | mk : List α → ListWrapper α

  inductive NatWrapper
    | mk : Nat → NatWrapper
\end{leancode}
%LEAN end BadExample

The only parameter to \leanm{List} is α, but the \leanm{cons} constructor takes a \leanm{List α} as second argument,
so \leanm{List} is not a shape type.
Similarly, \leanm{ListWrapper.mk} (resp. \leanm{NatWrapper.mk}) takes an argument of type \leanm{List α} (resp. \leanm{Nat}),
which are not type parameters, so these types are not shapes either.

Notice that shape types are non-recursive and do not depend on any other types, as a direct consequence 
of the definition. This makes them easy to translate into a polynomial functor. 

\begin{remark}
    One way to do this translation is to realize that all shape functors can be defined as a
    composition of sums and products. 
    This is similar to what the datatype package for Isabelle/HOL in~\cite{traytelCategoryTheoryBased} does.
    We'll use a different, slightly more monolithic approach.
\end{remark}

Recall that (multivariate) polynomial functors are defined as
\begin{leancode}
    structure MvPFunctor (n : Nat) :=
      (A : Type u) (B : A → TypeVec n)
\end{leancode}


Let us return to the example of sum and product types. 
For the \emph{head} type (\leanm{A} in the definition), we will take a type that has exactly as many constructors as
the shape type, but such that each constructor is a constant (i.e., takes no arguments).
Note that the head type does not take any type parameters. 

\begin{center}
    \begin{minipage}[t]{0.45\linewidth}
        \begin{leancode}
    inductive Sum.HeadT
      | inl : HeadT
      | inr : HeadT
        \end{leancode}
    \end{minipage}
    \begin{minipage}[t]{0.45\linewidth}
        \begin{leancode}    
    inductive Prod.HeadT
      | mk : HeadT
        \end{leancode}
    \end{minipage}
\end{center}

The \emph{child} family of types (\lean{B} in the definition) maps each constructor $c$ to a vector of types \leanm{α_c}.
What is most important is the cardinality of each type \leanm{α_c i}, 
because that is what determines the number of arguments of the $i$-th type parameter needed
to use constructor $c$. 

The concrete structure of these types is not relevant, so we'll always use \leanm{PFin2 m} (see \cref{sec:enhance:pfin2}), the type
of natural numbers less than $m$, to construct a type with cardinality $m$.

\pagebreak
\begin{remark}
    We could have used \lean{PFin2} for the head type as well, rather than generating bespoke inductive types.
    However, the current approach has the benefit that it's very clear which element of \lean{HeadT} represents which
    constructor.
\end{remark}

Let us start by counting for each constructor and each parameter type, how many times the constructor takes an argument of that type.
\begin{figure}[h]
    \begin{center}
        \begin{tabular}{l c c}
            Constructor     & α & β \\ \midrule
            \lean{Sum.inl}  & 1 & 0 \\
            \lean{Sum.inr}  & 0 & 1 \\
            \lean{Prod.mk}  & 1 & 1 \\    
        \end{tabular}    
    \end{center}

     \caption{Constructor argument bookkeeping}%
    \label{fig:ctor_bookkeepping}
\end{figure}


Using the counts of \cref{fig:ctor_bookkeepping}, we define the child family of types.

\begin{center}
  \begin{leancode}
    def Sum.ChildT : Sum.HeadT → TypeVec 2
      | .inl => ![PFin2 1, PFin2 0] -- the counts of α and β for Sum.inl 
      | .inr => ![PFin2 0, PFin2 1] -- the counts of α and β for Sum.inr 

    def Prod.ChildT : Prod.HeadT → TypeVec 2
      | .mk  => ![PFin2 1, PFin2 1]
  \end{leancode}
\end{center}

\begin{remark}
    If Lean knows which type to expect, say \lean{Sum.HeadT}, and we write an identifier with a leading
    dot, like \lean{.inl}, then it will automatically add the type as namespace, concluding that
    we must mean \lean{Sum.HeadT.inl}.
\end{remark}
From here on, the construction is the same for both types;
we'll show it just for \lean{Sum}.

\begin{leancode}
    def Sum.P  : MvPFunctor 2 := MvPFunctor.mk Sum.HeadT  Sum.ChildT
    
    def QpfSum.Uncurried : TypeFun 2 := MvPFunctor.Obj Sum.P
    def QpfSum : Type → Type → Type  := TypeFun.curried QpfSum.Uncurried
\end{leancode}

And we're done! However, these QPF-based versions of the types are still not very nice to use.
For example, if we want to construct a pair in \leanm{QpfProd}, we have to go through \leanm{MvPFunctor.mk},
which encodes its arguments in a not user-friendly way. 
Namely, to construct \lean{Sum α β} from an \lean{(a : α)}, i.e., use the \lean{inl} constructor, 
it expects something of type
\begin{center}
    \leanm{(Sum.ChildT .inl) ⟹ ![α, β]}
\end{center}
Recalling that \leanm{⟹} is shorthand for a vector of functions, we get
\begin{center}
  \leanm{(i : PFin 2) → (![PFin2 1, PFin 0] i → ![α, β] i)}
\end{center}
That is, \lean{inl} expects a function \lean{f : PFin2 1 → α} together with a function \lean{g : PFin2 0 → β}.
\begin{remark}
    Note that vectors are indexed right-to-left, so \lean{![α, β] 0} is \lean{β}
    and \lean{![α, β] 1} is \lean{α}
\end{remark}

Recall that we defined vectors of size $n$ as functions \lean{PFin2 n → α}, so, equivalently, \lean{f} is a vector of \lean{α}s of length 1 and \lean{g} is a trivial empty vector. So, really, the \lean{inl} constructor only needs a single argument of type \lean{α}, as desired. We can define a more convenient version of \lean{inl} as follows:

\begin{leancode}
    def QpfSum.inl {α β} (a : α) : QpfSum α β :=
      MvPFunctor.mk .inl (fun (i : PFin 2) => match i with
        | 1 => ![a]
        | 0 => ![]
      )
\end{leancode}

Similarly, the \lean{inr} constructor needs only a single argument of type \lean{β}.

\begin{leancode}
    def QpfSum.inr {α β} (b : β) : QpfSum α β :=
      MvPFunctor.mk .inr (fun (i : PFin 2) => match i with
        | 1 => ![]
        | 0 => ![b]
      )
\end{leancode}

By defining these constructors, we can make \lean{QpfSum} behave a bit more like the analogous \inductive{}
type.
Nonetheless, it is not possible to do pattern matching with these constructor wrappers, 
making \lean{QpfSum} slightly harder to use than the \inductive{} version.

\subsection*{Shape types as inductive types}
Because of the simplicity of shape types, i.e., their lack of any (co)induction or composition, 
they can just be defined as regular \inductive{} types.
However, to use shape types in subsequent constructions, e.g., taking a (co)fixpoint, 
needed to 
define more complicated (co)datatypes, we do need to show
that it is a QPF.\@

It stands to reason that if \lean{P} is a polynomial functor, and \lean{F} is isomorphic to \lean{P}, then \lean{F} is at
the very least a QPF.\footnote{Informally, we would call \lean{F} polynomial. But, recall that we formalized polynomial functors with a specific structure that might not be preserved by the isomorphism, so formally \lean{F} might not be considered polynomial.}
This observation is formalized as \lean{MvQpf.ofPolynomial}.

% \pagebreak
%LEANIGNORE
\begin{leancode}
    def ofPolynomial {F : TypeFun n} 
                     (P : MvPFunctor n) 
                     (box    : ∀{α}, F α → P.Obj α) 
                     (unbox  : ∀{α}, P.Obj α → F α) 
                     (box_unbox_id : ∀{α} (x : P.Obj α), box (unbox x) = x)
                     (unbox_box_id : ∀{α} (x : F α), unbox (box x) = x)
                  : MvQpf F
\end{leancode}

In the case of our \lean{Sum} example, we instantiate \lean{ofPolynomial} with 
\lean{F := TypeFun.ofCurried Sum} and \lean{P := Sum.P}.
To generate \lean{box} and \lean{unbox} we expand our bookkeeping a bit. For each constructor argument
we generate a fresh identifier, and while doing so we keep two lists: a list of all identifiers, in 
the order they were introduced, and a separate list for each parameter type, with just the identifiers for that type.

To illustrate, consider the following, slightly artificial, type
\begin{leancode}
  inductive SumOfPairs α β γ where
    | pairA     : α → α → SumOfPairs α β γ 
    | pairCandB : γ → β → SumOfPairs α β γ   -- note the order of β and γ
\end{leancode}

Following the procedure introduced at the start of this section, we obtain a corresponding polynomial functor.
\begin{leancode}
  inductive SumOfPairs.HeadT where
    | pairA     : HeadT
    | pairCandB : HeadT

  def SumOfPairs.ChildT : SumOfPairs.HeadT → TypeVec 3
    | .pairA      => ![PFin2 2, PFin2 0, PFin2 0]
    | .pairCandB  => ![PFin2 0, PFin2 1, PFin2 1]

  def SumOfPairs.P : MvPFunctor 3 :=
    ⟨SumOfPairs.HeadT, SumOfPairs.ChildT⟩
\end{leancode}

Let us use \lean{a₀} and \lean{a₁} for the arguments to the first constructor, and \lean{c} and \lean{b}
for the second constructor. Then, we separate the arguments by their type.

\begin{figure}[h]
\begin{center}
    \begin{tabular}{l c c}
        Type        & \lean{pairA}  & \lean{pairCandB}  \\ \midrule
        \emph{all}  & \lean{[a₀, a₁]} & \lean{[c, b]}   \\ \midrule
        \lean{α}    & \lean{[a₀, a₁]} & \lean{[]}       \\
        \lean{β}    & \lean{[]} & \lean{[b]}       \\
        \lean{γ}    & \lean{[]} & \lean{[c]}       \\
    \end{tabular}
\end{center}

\caption{Identifier bookkeeping for \texttt{\small SumOfPairs} constructors}%
\label{fig:fresh_ctor_vars}    
\end{figure}

The definition of \lean{box} follows fairly directly from \cref{fig:fresh_ctor_vars}, namely
\begin{leancode}
  def SumOfPairs.box : SumOfPairs α β γ → SumOfPairs.P.Obj ![α, β, γ]
    -- we pattern match using all variables for this constructor
    | .pairA a₀ a₁ => MvPFunctor.mk .pairA fun i => match i with
      | 2 => ![a₀, a₁]  -- α
      | 1 => ![]        -- β 
      | 0 => ![]        -- γ
    | .pairCandB c b => MvPFunctor.mk .pairCandB fun i => match i with
      | 2 => ![]        -- α 
      | 1 => ![b]       -- β 
      | 0 => ![c]       -- γ
\end{leancode}

Logically, \lean{unbox} does the reverse, using the bookkeeping in \cref{fig:fresh_ctor_vars} to determine
that for \lean{pairA} its arguments are stored at \lean{child 2 0} and \lean{child 2 1}---where the first index corresponds to the type (\lean{α}, \lean{β}, or \lean{γ}, indexed right-to-left) and the second number indexes the list of arguments specific to that type---while for
\lean{pairCandB} the arguments are found at \lean{child 0 0} and \lean{child 1 0}.
\begin{leancode}
    def SumOfPairs.unbox : SumOfPairs.P.Obj ![α, β, γ] → SumOfPairs α β γ
      := fun ⟨head, child⟩ => match head with
          | .pairA      => SumOfPairs.pairA (child 2 0) (child 2 1)
          | .pairCandB  => SumOfPairs.pairCandB (child 0 0) (child 1 0)
  \end{leancode}

The proof that these two functions are inverses is a straightforward mix of case distinction and reflexivity.
Finally, \lean{ofPolynomial} can be used to show that \lean{SumOfPairs} is a QPF.






\section{Recursive and corecursive types}%
\label{sec:rec_types}
If a type is recursive but otherwise does not mention other types, it is not strictly a shape type.
Nevertheless, by adding an extra type parameter \lean{ρ} and substituting \lean{ρ} for all (co)recursive occurrences of the type
to be defined, we obtain a shape type.

For example, the shape corresponding to \lean{List α} (as defined in \cref{sec:shape_types}) is
\begin{leancode}
  inductive List.Shape α ρ
    | nil  : Shape α ρ
    | cons : α → ρ → Shape α ρ
\end{leancode}

To get rid of the extra variable \lean{ρ}, we then take the fixpoint. Notice that it is required to show 
that \lean{List.Shape} is a QPF before we can do this.
\lean{List.Shape} is a shape type,
so we can derive an instance of \lean{MvQpf} following the procedure of the last section, whence the
following defines the type of finite lists:
\begin{leancode}
  def QpfList.Uncurried : TypeFun 2 
    := MvQpf.Fix (TypeFun.ofCurried List.Shape)
\end{leancode}



Conversely, suppose we wish to define \lean{CoList}, the \emph{coinductive} type of potentially infinite lists.
We introduced this type in the introduction with a specification that is similar to \lean{List}, but mandates a corecursive interpretation.
\begin{leancode}
  codata CoList α
    | nil  : CoList α
    | cons : α → CoList α → CoList α
\end{leancode}
There is no difference in the procedure to obtain the corresponding shape, we just replace all occurrences
of \lean{CoList α} as constructor argument type with a new type parameter \lean{ρ}, obtaining the exact same shape
as for \lean{List}.
\begin{leancode}
  inductive CoList.Shape α ρ
    | nil  : Shape α ρ
    | cons : α → ρ → Shape α ρ
\end{leancode}
But rather than taking the fixpoint, we take the \emph{cofixpoint} for a coinductive interpretation.
\begin{leancode}
  def CoList.Uncurried : TypeFun 2 
    := MvQpf.CoFix (TypeFun.ofCurried CoList.Shape)
\end{leancode}





\section{Composition pipeline}%
\label{sec:comp_pipeline}
The running example for this section will be the rose tree. Leaves of this tree are labelled with \lean{α}, while
internal nodes are labelled with \lean{β} and can have a finite, non-zero number of children.
\begin{leancode}
  data QpfTree α β
    | leaf : α → QpfTree α β
    | node : β → QpfTree α β → QpfList (QpfTree α β) → QpfTree α β
\end{leancode}

The type is recursive, so we introduce the fresh parameter as before, taking care to also substitute it in 
subexpressions.
\begin{leancode}
  data QpfTree.Nonrecursive α β ρ
    | leaf : α → Nonrecursive α β ρ
    | node : β → ρ → QpfList ρ → Nonrecursive α β ρ
\end{leancode}

However, the result is not quite a shape type yet, we also have to get rid of \lean{QpfList ρ} as an
argument type. To do so, we simply introduce more parameters, while remembering which type these
new parameters are supposed to stand for.

\begin{leancode}
  inductive QpfTree.Shape α β ρ σ₁
    | leaf : α → QpfTree α β ρ σ₁
    | node : β → ρ → σ₁ → QpfTree α β ρ σ₁
\end{leancode}

Then, proceed as in \cref{sec:shape_types} to show that \lean{QpfTree.Shape} is a QPF.\@

\begin{remark}
  When doing this substitution, we can reuse the same parameter for multiple occurrences of the same type.
  Suppose we had a constructor that takes two lists, like
  %LEANIGNORE
  \begin{leancode}
    | node₂ : β → ρ → QpfList ρ → QpfList ρ → QpfTree α β ρ
  \end{leancode}
  Then we can reuse the same fresh parameter \lean{σ₁} for both occurrences.
  %LEANIGNORE
  \begin{leancode}
    | node₂ : β → ρ → σ₁ → σ₁ → QpfTree α β ρ σ₁
  \end{leancode}
  
  On the other hand, if a non-parameter type also occurs as a subexpression of another type, then
  we will not substitute it with the same parameter.
  The example gets a bit contrived, but suppose nodes consist of both a list of children and a nested list 
  of lists of children.
  %LEANIGNORE
  \begin{leancode}
    | node₃ : β → ρ → QpfList ρ → QpfList (QpfList ρ) → QpfTree α β ρ
  \end{leancode}
  This is translated to
  %LEANIGNORE
  \begin{leancode}
    | node₃ : β → ρ → σ₁ → σ₂ → QpfTree α β ρ σ₁ σ₂
  \end{leancode}
  Where \lean{σ₁} stands for \lean{QpfList ρ}, as before, and \lean{σ₂} stands for \lean{QpfList (QpfList ρ)}, \emph{not} \lean{QpfList σ₁}.    
  
  Do note that this does \emph{not} apply when we are adding a new parameter for recursive occurrences in the very first step (like we did by adding \lean{ρ} to obtain the \lean{Nonrecursive} specification), such variables \emph{do} get substituted in all subexpressions.
  \end{remark}

Returning to \lean{QpfTree.Shape}, parameter \lean{σ₁} is supposed to stand for \lean{QpfList ρ}, so we're aiming to define a QPF which satisfies:
%LEANIGNORE
\begin{leancode}
  Base α β ρ = QpfTree.Shape α β ρ (QpfList ρ)
\end{leancode}


The \emph{composition pipeline} translates such an equation to a definition of \lean{Base} in terms of the appropriate construction on QPF, such that: (a) \lean{Base} is known to be a 3-ary QPF, and (b) the desired equality indeed holds.
The composition pipeline is not just an internal detail of the procedure, we also expose it through the \qpf{} command, whose syntax is inspired by \keyword{def}.
\begin{leancode}
  qpf Base α β ρ := QpfTree.Shape α β ρ (QpfList ρ)
\end{leancode}



As the name composition pipeline alludes, we are interested in defining compositions of QPFs. Formally, \lean{MvQpf.Comp} has the signature:
\begin{center}
    \lean{TypeFun n → Vec (TypeFun m) n → TypeFun m}
\end{center}
That is, an $n$-ary type function \lean{F} is composed with an $n$-sized vector of $m$-ary type functions, resulting in an $m$-ary type function. The composition is essentially defined as
%LEANIGNORE
\begin{leancode}
    (Comp F ![G₁, ..., Gₙ]) ![α₁, ..., αₘ] 
                = F ![G₁ ![α₁, ..., αₘ], ..., Gₘ ![α₁, ..., αₘ]]
\end{leancode}
All arguments \lean{αᵢ} are broadcast to all functors \lean{Gⱼ}, meaning we don't have to worry about argument duplication or reordering.

Continuing with the motivating example, we are going to define \lean{Base} as \lean{Comp QpfTree.Shape ![G₁, G₂, G₃, G₄]}, for some functors \lean{G₁}, \lean{G₂}, \lean{G₃} and \lean{G₄}, satisfying the following equalities:
\begin{leancode}
  qpf G₁ α β ρ := α                qpf G₃ α β ρ := ρ
  qpf G₂ α β ρ := β                qpf G₄ α β ρ := (QpfList ρ)
\end{leancode}

Which we will recursively determine.
Before we continue, it should be clarified that the composition pipeline is not able to satisfy all 
equations. For example, it is impossible to define a QPF that satisfies
\begin{badleancode}
  qpf H α β := α → β 
\end{badleancode}
This is because the arrow type constructor \lean{(⋅ → ⋅)} is \emph{not} functorial. 
At least, it is not functorial in both arguments. If instead, we fix any value \lean{α} for the
first argument, we obtain \lean{(α → ⋅)}, which \emph{is} a QPF.\@
More generally, we say that in \lean{α → β}, the variable \lean{α} is \emph{dead}, since it occurs as a non-functorial argument, while β is a \emph{live} variable.

By default, the composition pipeline assumes arguments are live, but we can explicitly mark some as dead
by giving a type ascription. 
\begin{leancode}
  qpf H (α : Type u) β = α → β 
\end{leancode}
The result is a family of QPFs, which means \lean{H} is not a QPF, but \lean{H a}, for arbitrary values \lean{a : α} is.

\begin{remark}
  Recall that the live parameters to a functor must all live in the same universe \leanm{Type u}, where
  \lean{u} can be either some fixed level, or universe polymorphic.
  To make the implementation simpler, we don't allow any type ascription for live variables in the 
  prototype implementation, instead relying on inference to determine what universe they should live in.
  This frees up the type ascription as a simple (to implement) way of distinguishing live and dead
  variables.

  In a more polished future implementation, we should probably move to a more explicit syntax to mark
  dead variables, to minimize the risk of variables that could be live unintentionally being marked dead.
\end{remark}



Continuing on, the composition pipeline does support these three kinds of functors:
\begin{itemize}
    \item \emph{Projections: } The target expression is just a parameter, as in \lean{G₁ α β ρ = α}.
    \item \emph{Constants: } The target expression does not mention the live parameters at all, e.g., \lean{G α β = Nat} or 
                \leanm{G (n : Nat) β = PFin2 n}.
    \item \emph{Compositions: } The target expression is an application of a QPF.\@
\end{itemize}

This notably excludes dependent arrows \lean{(a : α) → \_}, anonymous functions \leanm{fun γ => _},
and applications that cannot be appropriately broken down (such as \lean{α → β} when \lean{α} is a live parameter).



Projections are trivial to identify and are represented with \lean{MvQpf.Prj n i}, 
where $n$ is the arity and $i$ the index of the parameter to project to (counted right-to-left).
\begin{leancode}
  qpf G₁ α β ρ := α  --> def G₁ := @MvQpf.Prj 3 2
  qpf G₂ α β ρ := β  --> def G₂ := @MvQpf.Prj 3 1
  qpf G₃ α β ρ := ρ  --> def G₃ := @MvQpf.Prj 3 0
\end{leancode}


For constants, we only have to verify that the target expression does not depend on live parameters,
then we translate them into \lean{@MvQpf.Const n τ}, with $n$ again the arity and \lean{τ} the constant type. 
\begin{leancode}
  qpf C₁ α β γ       := Nat     --> def C₁ := @MvQpf.Const 3 Nat

  qpf C₂ (n : Nat) α := PFin2 n 
      --> def C₂ (n : Nat) := @MvQpf.Const 1 (PFin2 n)
\end{leancode}
In the second example, \lean{PFin2 n} is not strictly a constant---it depends on \lean{n}---but it is still
represented with a constant functor because $n$ is marked a dead variable.

The last kind of supported expressions are applications \lean{G e₁ ... eₖ} where \lean{G} is a $k$-ary QPF
that doesn't depend on live parameters, for arbitrary $k$.
This is the kind of expression we discussed at the start of this section.
We can represent them as \lean{MvQpf.Comp} with recursively determined functors representing arguments \lean{eᵢ}.
Note that \lean{G} is allowed to be an application itself, so long as it fits the set criteria (it is a QPF and 
does not depend on live variables), so this breakdown is not necessarily unique.

As an optimization, we don't have to generate \keyword{def} declarations for all intermediate functors, we can just
inline them.
\begin{leancode}
  qpf G₄ α β ρ := QpfList ρ 
    --> def G₄ α β ρ := MvQpf.Comp QpfList ![@MvQpf.Prj 3 0]

  qpf Base α β ρ := QpfTree.Shape α β ρ (QpfList ρ)
    --> def Base α β ρ := MvQpf.Comp QpfTree.Shape ![
    -->     @MvQpf.Prj 3 2,                       -- G₁ 
    -->     @MvQpf.Prj 3 1,                       -- G₂ 
    -->     @MvQpf.Prj 3 0,                       -- G₃
    -->     MvQpf.Comp QpfList ![@MvQpf.Prj 3 0], -- G₄
    --> ]
\end{leancode}



Finally, the desired type is obtained by taking the fixpoint. Here, too, we can inline the definition
of \lean{Base}.
\begin{leancode}
  def QpfTree α β : TypeFun 2 :=
    MvQpf.Fix (MvQpf.Comp QpfTree.Shape ![
        @MvQpf.Prj 3 2,                       -- G₁ 
        @MvQpf.Prj 3 1,                       -- G₂ 
        @MvQpf.Prj 3 0,                       -- G₃
        MvQpf.Comp QpfList ![@MvQpf.Prj 3 0], -- G₄
    ])
\end{leancode}




\section{Auxiliary constructions}%
\label{sec:aux_constructions}

After constructing the (co)datatype, there are a few extra definitions we want to add to make the result more usable. To begin with, we want to generate a function for each constructor 
in the specification, with the same name and type. Generating these is relatively straightforward.
For example, let us recall the specification for lists.
\begin{leancode}
  data List α
    | nil  : List α 
    | cons : α → List α → List α
\end{leancode}
Following the steps described so far defines \lean{List} in terms of a generated shape type \lean{List.Shape}
and a fixpoint. The constructors we want to generate are simple wrappers around these.
\begin{leancode}
  def List.nil : List α 
    := MvQpf.Fix.mk List.Shape.nil

  def List.cons : α → List α → List α 
    := fun a as => MvQpf.Fix.mk (List.Shape.cons a as)
\end{leancode}
Effectively, we are just applying \lean{Fix.mk} to the result of the corresponding constructor of \lean{List.Shape}.
The same method applies to codatatypes, using \lean{CoFix.mk} instead.

At this point, we would also want to generate specialized (co)recursion principles and \lean{noConfusion}
theorems, but as mentioned in the introduction, we don't support doing so yet.


% TODO: check if this pagebreak still makes sense
% \pagebreak[3]
\section{Final overview}%
\label{sec:procedure:overview}

To sum up, we can give a high-level overview of the procedure in the following steps.
\begin{enumerate}
  \item\label{enum:proc:make_nonrec}
    Make the specification non-recursive by introducing a new parameter (\cref{sec:rec_types})
  \item\label{enum:proc:mkShape} 
    Introduce more parameters, to obtain a shape type \lean{Shape} (\cref{sec:comp_pipeline})
  \item\label{enum:proc:mkQpf} 
    Show that \lean{Shape} is a QPF, by (\cref{sec:shape_types})
        \begin{enumerate}
          \item Deriving an equivalent polynomial functor, and
          \item Showing that this polynomial functor is isomorphic to \lean{Shape}
        \end{enumerate}
  \item\label{enum:proc:mkBase} 
    Use the composition pipeline to solve for the intended values for parameters introduced in step 2, producing the \lean{Base} functor (\cref{sec:comp_pipeline})
  \item\label{enum:proc:fixpoint} 
    Take the fixpoint (or cofixpoint) of \lean{Base}, getting rid of the parameter introduced in step 1 
    and producing the desired functor (\cref{sec:rec_types})
  \item\label{enum:proc:aux} 
    Generate auxiliary constructions (\cref{sec:aux_constructions})
\end{enumerate}

Steps~\ref{enum:proc:make_nonrec}~and~\ref{enum:proc:fixpoint} can be omitted if the specification is not (co)recursive, and steps~\ref{enum:proc:mkShape}~and~\ref{enum:proc:mkBase} may be omitted
if the input is already a shape type, but it is also fine to forgo such analysis and perform all steps anyway.


